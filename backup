#!/bin/env zsh
set -eo pipefail

typeset -A PARTITIONS
PARTITIONS=([root]=/ [home]=/home)

DRIVE=/backups
DESTINATION=$DRIVE/archives/$(date +%Y/%m/%d)
CHECKSUM_FILE=checksum.txt

function log() {
  echo "[$(date +%T)] ${@}"
}

# usage: create_archive <destination> <source>
function create_archive() {
  local dst=$1.tar.bz2
  local src=$2

  if [[ -s $dst ]]; then
    echo "$dst already exists. Skipping."
    return
  fi

  # if any more directories or files are excluded
  # make sure to update the `restore` script so we
  # recreate the directories.
  tar --create --preserve-permissions --bzip2 \
    --file=$dst \
    --exclude=/proc \
    --exclude=/tmp \
    --exclude=/mnt \
    --exclude=/dev \
    --exclude=/sys \
    --exclude=/run \
    --exclude=/media \
    --exclude=/var/log \
    --exclude=/var/cache/apt/archives \
    --exclude='/usr/src/linux-headers*' \
    --exclude='/home/*/.cache' \
    --exclude='/home/*/.local/share/Trash' \
    --exclude=/var/lib/snapd/cache \
    --one-file-system $src
}

# calculate hash of new archive and store it in the
# information text file with the archive
#
# usage: create_checksum <destination> <archive>
function create_checksum() {
  local destination=$1
  local archive=$2
  local info_file=$destination/$CHECKSUM_FILE

  if [[ ! -e $info_file ]]; then
    touch $info_file
  fi

  b2sum ${destination}/${archive}.tar.bz2 >> $info_file
}

if ! (mountpoint --quiet $DRIVE); then
  echo "backup: error - $@" 1>&2
  exit 1
fi

log "Starting backups"

for type in ${(k)PARTITIONS}; do
  log "Creating ${type} archive" 
  # Create the necessary files in the $DRIVE/archives directory.
  # The archives should be split up like so /<year>/<month>/<day/
  # archives
  # └── 2022
  #     └── 04
  #         └── 12
  #             ├── home.tar.bz2
  #             ├── root.tar.gz
  #             └── checksum.txt
  if [[ ! -d $DESTINATION ]]; then
    mkdir --parents $DESTINATION
  fi

  # Back that shit up!
  create_archive $DESTINATION/$type ${PARTITIONS[$type]}

  log "Archive complete, adding checksum hash"
  create_checksum $DESTINATION $type
done

log "Finished."

# this command should be run by root so make sure I own everything
chown --recursive skip:skip $DRIVE/archives/$(date +%Y/%m)


exit 0
